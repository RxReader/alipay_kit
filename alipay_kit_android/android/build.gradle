File pubspec = new File(project.projectDir.parentFile,  'pubspec.yaml')
String yaml = pubspec.text
// Using \s*['|"]?([^\n|'|"]*)['|"]? to extract version number.
java.util.regex.Matcher versionMatcher = java.util.regex.Pattern.compile("^version:\\s*['|\"]?([^\\n|'|\"]*)['|\"]?\$", java.util.regex.Pattern.MULTILINE).matcher(yaml)
versionMatcher.find()
String library_version = versionMatcher.group(1).replaceAll("\\+", "-")

group 'io.github.v7lin.alipay_kit_android'
version library_version

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()

        // 私仓
        flatDir {
            dirs project(':alipay_kit_android').file('libs')
        }
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 19

        // library 混淆 -> 随 library 引用，自动添加到 apk 打包混淆
        consumerProguardFiles 'consumer-rules.pro'
    }

    flavorDimensions 'vendor'

    productFlavors {
        vendor {
            dimension 'vendor'

            // library 混淆 -> 随 library 引用，自动添加到 apk 打包混淆
            consumerProguardFiles 'consumer-vendor-rules.pro'
        }
    }
}

def use_utdid() {
    if (rootProject.ext.has('alipay_kit_flavor') && rootProject.ext.get('alipay_kit_flavor') == 'noutdid') {
        return false
    }
    return true
}

dependencies {
    // v15.8.10
    if (use_utdid()) {
        vendorImplementation 'com.alipay.sdk:alipaysdk-android:15.8.10@aar'
    } else {
        vendorImplementation(name: 'alipaysdk-noutdid-15.8.10.220419171600', ext: 'aar')
    }
}
